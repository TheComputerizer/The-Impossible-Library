//file:noinspection GroovyUnusedAssignment
//file:noinspection GroovyAssignabilityCheck
plugins {
    id 'fabric-loom' version '1.0-SNAPSHOT'
    id 'maven-publish'
}
version = '1.19.2-fabric-0.2.2'
group = 'mods.thecomputerizer.theimpossiblelibrary'
archivesBaseName = 'theimpossiblelibrary'

java.toolchain.languageVersion = JavaLanguageVersion.of(17)

repositories {
    google()
    mavenCentral()
    maven {
        name 'ParchmentMC'
        url = uri('https://maven.parchmentmc.org')
    }
    maven {
        url = uri("https://m2.dv8tion.net/releases")
    }
}

sourceSets.main.resources { srcDir 'src/generated/resources' }


dependencies {
    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings loom.layered() {
        officialMojangMappings()
        parchment("org.parchmentmc.data:parchment-1.19.2:2022.11.27@zip")
    }
    modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"
    modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"

    //external dependencies
    include (implementation("com.moandjiezana.toml:toml4j:0.7.2") {
        exclude group: 'com.google.code.gson', module: 'gson'
    })
}

processResources {
    inputs.property "version", project.version
    filesMatching("fabric.mod.json") {
        expand "version": project.version
    }
}

java {
    withSourcesJar()
}

jar {
    from("LICENSE") {
        rename { "${it}_${project.archivesBaseName}"}
    }
}