/**
 * Build workflow guidelines
 * api -> (fabric, forge, legacy, neoforge) -> (1.12, 1.16, 1.18, 1.19, 1.20, 1.21) -> (minor versions) -> main
 * Shading and relocation must happen within each module to properly respect reobfuscation.
 * Any necessary reobfuscations must happen in the version the deobfuscations happened in.
 */

apply from: rootProject.file('buildSrc\\shared.gradle')
java.toolchain.languageVersion = JavaLanguageVersion.of(23)

def fabric = project(':fabric')
def legacy_1_12_2 = project(':legacy:1.12')
def fabric_1_16_5 = project(':fabric:fabric_1.16')
def forge_1_16_5 = project(':forge:forge_1.16')

processResources {
    fabric.afterEvaluate { from(it.processResources.outputs) }
}

processTestResources {
    fabric.afterEvaluate { from(it.processTestResources.outputs) }
}

jar {
    legacy_1_12_2.afterEvaluate { from(it.shadowJar.archiveFile.collect { zipTree(it) }) }
    fabric_1_16_5.afterEvaluate { from(it.remapJar.archiveFile.collect { zipTree(it) }) }
    forge_1_16_5.afterEvaluate { from(it.shadowJar.archiveFile.collect { zipTree(it) }) }
    manifest {
        attributes([
                'Implementation-Title': mod_name,
                'Implementation-Version': mod_version,
                'TILMultiversionCoreMods': core_entrypoint,
                'TILMultiversionMods': common_entrypoint
        ])
    }
}

[ ':legacy:1.12:build',
  ':fabric:fabric_1.16:build',
  ':forge:forge_1.16:build' ].forEach { tasks.jar.dependsOn(it) }