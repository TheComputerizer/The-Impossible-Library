plugins {
    id 'com.github.johnrengelman.shadow' version '8.1.+'
    id 'net.minecraftforge.gradle' version '6.+'
    id 'wtf.gofancy.fancygradle' version '1.1.+'
}

fancyGradle {
    patches {
        resources
        coremods
        asm
        mergetool
    }
}

java.toolchain.getLanguageVersion().set(JavaLanguageVersion.of(21))

minecraft {
    mappings channel: 'stable', version: mappings_version
    accessTransformer = file("src/main/resources/META-INF/${mod_accesstransformer}")
    runs {
        def args = [
                '-DtilDev=true',
                '-DtilClassPathMods=mods.thecomputerizer.theimpossiblelibrary.api.common.TILCommonEntryPoint'
        ]
        client {
            workingDirectory file('run_client')
            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'
            property 'forge.logging.console.level', 'debug'
            jvmArgs args
        }
        server {
            workingDirectory file('run_server')
            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'
            property 'forge.logging.console.level', 'debug'
            jvmArgs args
        }
    }
}

configurations {
    shade
    implementation.extendsFrom(shade)
    shadow.extendsFrom(shade)
    shade.getIncoming().afterResolve {
        files.collect { it.isDirectory() ? it : zipTree(it) } { exclude 'module-info.class' }
    }
    //shade.resolutionStrategy.dependencySubstitution {}
    //shade.getIncoming().getFiles().forEach { it.isDirectory() ? it : zipTree(it) }
    //shade.getIncoming().getFiles().asCollection().forEach { it.isDirectory() ? it : zipTree(it) } {
        //exclude 'module-info.class'
    //}
}

repositories {
    maven {
        name = 'CurseMaven'
        url = uri('https://www.cursemaven.com')
        content {
            includeGroup 'curse.maven'
        }
    }
    maven {
        name = 'BlameJared'
        url = uri('https://maven.blamejared.com')
        content {
            includeGroup 'net.darkhax.bookshelf'
            includeGroup 'net.darkhax.gamestages'
        }
    }
}

dependencies {
    //noinspection VulnerableLibrariesLocal
    minecraft("net.minecraftforge:forge:${minecraft_version}-${forge_version}")

    annotationProcessor("com.google.code.gson:gson:${gson_annotation}")
    annotationProcessor("com.google.guava:guava:${guava_annotation}")
    annotationProcessor("org.ow2.asm:asm-debug-all:${asm_debug_annotation}")
    annotationProcessor("org.projectlombok:lombok:${lombok_version}")

    compileOnly("org.projectlombok:lombok:${lombok_version}")
    //TODO Figure out if this works in dev
    compileOnly(fg.deobf("curse.maven:dsurround-238891:${dsurround_version}"))
    compileOnly(fg.deobf("curse.maven:orelib-307806:${orelib_version}"))

    implementation project(':legacy')
    //mod support dependencies
    implementation(fg.deobf("curse.maven:bloodmoon-226321:${bloodmoon_version}"))
    implementation(fg.deobf("net.darkhax.bookshelf:Bookshelf-${minecraft_version}:${bookshelf_version}"))
    implementation(fg.deobf("curse.maven:champions-307152:${champions_version}"))
    implementation(fg.deobf("curse.maven:coroutil-237749:${coroutil_version}"))
    implementation(fg.deobf("net.darkhax.gamestages:GameStages-${minecraft_version}:${gamestages_version}"))
    implementation(fg.deobf("curse.maven:infernalmobs-227875:${infernal_version}"))
    implementation(fg.deobf("curse.maven:nyx-349214:${nyx_version}"))
    implementation(fg.deobf("curse.maven:sereneseasons-291874:$seasons_version}"))
    implementation(fg.deobf("curse.maven:weather2-695602:${weather_version}"))

    runtimeOnly(fg.deobf("curse.maven:hei-557549:${hei_version}"))

    shade("com.moandjiezana.toml:toml4j:${toml4j_version}") { transitive = false }
    shade("org.joml:joml:${joml_version}")
}

shadowJar {
    //relocate shaded dependencies to the shadow package to prevent library conflicts
    relocate 'com.moandjiezana.toml', "${shadow_package}.com.moandjiezana.toml"
    relocate 'org.joml', "${shadow_package}.org.joml"
    configurations = [project.configurations.shadow]
    setArchiveClassifier('')
    exclude 'module-info.class'
}

reobf {
    shadowJar {
        dependsOn createMcpToSrg
        mappings = createMcpToSrg.output
    }
}

processResources {
    duplicatesStrategy = DuplicatesStrategy.INCLUDE
    var replacements = [
            'minecraft_version': minecraft_version,
            'mod_author': mod_authors,
            'mod_description': mod_description,
            'mod_license': mod_license,
            'mod_id': mod_id,
            'mod_name': mod_name,
            'mod_version': version
    ]
    inputs.properties replacements
    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
        expand replacements
    }
    from(sourceSets.main.resources) exclude 'mcmod.info'
    //exclude '**/theimpossiblelibrary/test/**'
}

jar {
    manifest {
        attributes([
                'FMLAT': mod_accesstransformer,
                'FMLCorePlugin': "${module_package}.${mod_plugin}1_12_2",
                'FMLCorePluginContainsFMLMod': true,
                'ForceLoadAsMod': true,
                "Implementation-Title": project.name,
                'TILMultiversionMods': 'mods.thecomputerizer.theimpossiblelibrary.api.common.TILCommonEntryPoint'
        ])
    }
}

tasks.withType(JavaCompile).configureEach {
    javaCompiler = javaToolchains.compilerFor {
        languageVersion = JavaLanguageVersion.of(8)
    }
}

tasks.withType(JavaExec).configureEach {
    javaLauncher = javaToolchains.launcherFor {
        languageVersion = JavaLanguageVersion.of(8)
    }
}

tasks.withType(Test).configureEach {
    javaLauncher = javaToolchains.launcherFor {
        languageVersion = JavaLanguageVersion.of(8)
    }
}

tasks.named('compileJava',JavaCompile) {
    source(project(":api").sourceSets.main.allSource)
}

tasks.named('compileTestJava',JavaCompile) {
    source(project(":api").sourceSets.test.allJava)
}

tasks.withType(Javadoc).configureEach {
    source(project(":api").sourceSets.main.allJava)
}

tasks.withType(Jar).configureEach {
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    from(project(":api").sourceSets.main.allSource)
}

tasks.withType(ProcessResources).configureEach {
    from(project(":api").sourceSets.main.resources)
}

tasks.assemble.dependsOn('shadowJar')
tasks.shadowJar.dependsOn('reobfJar')
tasks.jar.finalizedBy('reobfJar')