//file:noinspection GroovyAssignabilityCheck
buildscript {
    repositories {
        gradlePluginPortal() {
            content {
                [ 'com.fasterxml', 'com.fasterxml.jackson', 'com.fasterxml.woodstox', 'com.google.code.findbugs',
                  'com.google.code.gson', 'com.google.errorprone', 'com.google.guava', 'com.google.j2objc',
                  'com.machinezoo.noexception', 'commons-codec', 'commons-io', 'commons-logging', 'de.siegmar',
                  'io.fabric8', 'io.github.goooler.shadow', 'io.netty', 'it.unimi.dsi', 'jakarta.platform',
                  'net.sf.jopt-simple', 'org.apache', 'org.apache.ant', 'org.apache.commons',
                  'org.apache.httpcomponents', 'org.apache.logging', 'org.apache.logging.log4j', 'org.apache.maven',
                  'org.checkerframework', 'org.codehaus.groovy', 'org.codehaus.plexus', 'org.codehaus.woodstox',
                  'org.eclipse.ee4j', 'org.eclipse.jetty', 'org.jdom', 'org.junit', 'org.mockito', 'org.ow2',
                  'org.ow2.asm', 'org.slf4j', 'org.sonatype.oss', 'org.springframework', 'org.tukaani',
                  'org.vafer' ].forEach { includeGroup it }
            }
        }
        maven {
            name = 'Forge'
            url = uri('https://maven.minecraftforge.net/')
            content {
                [ 'net.minecraftforge', 'net.minecraftforge.gradle' ].forEach{ includeGroup it }
            }
        }
        maven {
            name = 'ParchmentMC'
            url = uri('https://maven.parchmentmc.org')
            content {
                [ 'org.parchmentmc', 'org.parchmentmc.feather',
                  'org.parchmentmc.librarian.forgegradle' ].forEach { includeGroup it }
            }
        }
    }
    dependencies {
        classpath("${shadow_plugin}:${shadow_version}")
        classpath("${forge_gradle_plugin}:${forge_gradle_version}")
        classpath("${parchment_plugin}:${parchment_plugin_version}")
    }
}

apply from: rootProject.file('gradle\\java8-module.gradle')
apply from: rootProject.file('gradle\\shadow-relocate.gradle')

plugins.apply('net.minecraftforge.gradle')
plugins.apply('org.parchmentmc.librarian.forgegradle')

minecraft {
    mappings channel: 'parchment', version: "${parchment_1_16_5}-${minecraft_1_16_5}"
    runs {
        def args = [ dev_arg, common_entrypoint_arg, core_entrypoint_arg ]
        configureEach {
            def srgToMcpFile = project.tasks.createSrgToMcp.outputs.files[0].path
            property 'net.minecraftforge.gradle.GradleStart.srg.srg-mcp', srgToMcpFile
            property 'mixin.env.remapRefMap', 'true'
            property 'mixin.env.refMapRemappingFile', srgToMcpFile
        }
        client {
            workingDirectory file('run_client')
            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'
            property 'forge.logging.console.level', forge_log_level
            jvmArgs args
            mods {
                theimpossiblelibrary {
                    source sourceSets.main
                }
            }
        }
        server {
            workingDirectory file('run_server')
            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'
            property 'forge.logging.console.level', forge_log_level
            jvmArgs args
            mods {
                theimpossiblelibrary {
                    source sourceSets.main
                }
            }
        }
    }
}

repositories {
    maven {
        name = 'CurseMaven'
        url = uri('https://www.cursemaven.com')
        content {
            includeGroup 'curse.maven'
        }
    }
    maven {
        name = 'BlameJared'
        url = uri('https://maven.blamejared.com')
        content {
            [ 'bookshelf', 'gamestages' ].forEach {includeGroup "net.darkhax.${it}"}
        }
    }
    maven {
        name = 'Modding Legacy'
        url = uri('https://maven.moddinglegacy.com/maven')
        content {
            includeGroup 'com.legacy'
        }
    }
    maven {
        name = 'ModMaven'
        url = uri('https://modmaven.dev')
        content {
            includeGroup( 'mezz.jei')
        }
    }
}

def shared = project(':shared:1.16')

dependencies {
    //noinspection VulnerableLibrariesLocal
    minecraft("${forge_mc}:${forge_1_16_5}")

    implementation(project(':api'))
    implementation(shared)
    implementation(project(':forge'))

    //mod support dependencies
    implementation(fg.deobf("${betterweather_artifact}:${betterweather_1_16_5}"))
    implementation(fg.deobf("${blueskies_artifact}:${blueskies_1_16_5}"))
    implementation(fg.deobf("${bookshelf_artifact}-${bookshelf_1_16_5}"))
    implementation(fg.deobf("${champions_artifact}:${champions_1_16_5}"))
    implementation(fg.deobf("${configured_artifact}:${configured_1_16_5}"))
    implementation(fg.deobf("${dynamic_surroundings_artifact}:${dynamic_surroundings_1_16_5}"))
    implementation(fg.deobf("${enhanced_celestials_artifact}:${enhanced_celestials_1_16_5}"))
    implementation(fg.deobf("${game_stages_artifact}-${game_stages_1_16_5}"))
    implementation(fg.deobf("${serene_seasons_artifact}:${serene_seasons_1_16_5}"))
    implementation(fg.deobf("${structure_gel_api_artifact}:${structure_gel_api_1_16_5}"))

    runtimeOnly(fg.deobf("${jei_artifact}:${jei_1_16_5}"))
}

compileJava {
    source(project(':api').sourceSets.main.allJava,
            project(':forge').sourceSets.main.allJava)
}

compileTestJava {
    source(project(':api').sourceSets.test.allJava,
            project(':forge').sourceSets.test.allJava)
}

processResources {
    from(project(':forge').processResources.outputs)
}

processTestResources {
    from(project(':forge').processTestResources.outputs)
}

jar {
    from(project(':forge').jar.archiveFile.collect { zipTree(it) })
}

reobf {
    shadowJar {
        dependsOn createMcpToSrg
        mappings = createMcpToSrg.output
    }
}

shadowJar {
    from(project(':api').jar.archiveFile.collect { zipTree(it) })
    shared.afterEvaluate {
        from(shared.jar.archiveFile.collect { zipTree(it) })
        relocate "${shared_base_package}", "${forge_base_package}"
    }
}

tasks.assemble.dependsOn(shadowJar)
tasks.shadowJar.dependsOn('reobfJar')
tasks.jar.finalizedBy('reobfJar')
tasks.jar.dependsOn(':forge:build')